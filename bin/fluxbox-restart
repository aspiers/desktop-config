#!/bin/zsh

# Needed to fix corrupted xfce4-panel which happens after display
# reconfiguration.  Solution is to switch back to xfwm4 for a second,
# then back to fluxbox.

# NOTE! This relies on the XFCE session manager not immediately
# automatically restarting xfwm4 after it's pkilled.

# set -x
: $0 pid is $$

is_running () {
    pgrep -lx "$1" # >/dev/null
}

ensure_stopped () {
    while is_running "$1"; do
        : "$1 is running"
        sleep 0.5
    done
}

ensure_started () {
    while ! is_running "$1"; do
        : "$1 is not running"
        sleep 0.5
    done
}

if [ -n "$SSH_CONNECTION" ]; then
    # This can break stuff launched from the fluxbox menus which
    # relies on get-connection-type, e.g. switch-feed-emacs
    echo >&2 "\$SSH_CONNECTION is set to $SSH_CONNECTION; cowardly aborting."
    exit 1
fi

if is_running fluxbox; then
    pkill -x fluxbox
    ensure_stopped fluxbox
    ( xfwm4 & )
    ensure_started xfwm4
fi

if is_running xfwm4; then
    pkill xfwm4
    ensure_stopped xfwm4
fi

fluxbox -log ~/.log/fluxbox.log >>& ~/.log/fluxbox.log &!

# Kill annoying popup
sleep 1
xdotool search \
        --onlyvisible \
        --screen 0 \
        --class \
        --classname 'Wrapper-2.0' \
        getwindowpid |
    xargs kill
